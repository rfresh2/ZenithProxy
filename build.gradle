/*
 * Adapted from the Wizardry License
 *
 * Copyright (c) 2016-2020 DaPorkchop_
 *
 * Permission is hereby granted to any persons and/or organizations using this software to copy, modify, merge, publish, and distribute it.
 * Said persons and/or organizations are not allowed to use the software or any derivatives of the work for commercial use or any other means to generate income, nor are they allowed to claim this software as their own.
 *
 * The persons and/or organizations are also disallowed from sub-licensing and/or trademarking this software without explicit permission from DaPorkchop_.
 *
 * Any persons and/or organizations using this software must disclose their source code and have it publicly available, include this license, provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

apply plugin: "java"
apply plugin: "maven-publish"

ext {
    //general things
    javaVersion = "1.8"
    minecraftVersion = "1.12.2"
    programVersion = "0.2.5"

    //dependency things
    apachecommonstextVersion = "1.3"
    javawebsocketVersion = "1.4.0"
    junitVersion = "4.12"
    lombokVersion = "1.16.20"
    mcauthlibVersion = "91b624ec36"
    mcprotocollibVersion = "83221af168"
    porklibVersion = "0.5.3-SNAPSHOT"
}

group "net.daporkchop"
version "$programVersion-$minecraftVersion"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io/"
    }
}

configurations {
    shade
    compile.extendsFrom shade

    all {
        resolutionStrategy {
            force "com.github.steveice10:mcauthlib:$mcauthlibVersion"
        }
    }
}

dependencies {
    shade "net.daporkchop.lib:gui:$porklibVersion"
    shade "net.daporkchop.lib:hash:$porklibVersion"
    shade "net.daporkchop.lib:logging:$porklibVersion"
    shade "net.daporkchop.lib:primitive:$porklibVersion"
    shade "net.daporkchop.lib:minecraft-text:$porklibVersion"

    shade ("com.github.Steveice10:MCProtocolLib:$mcprotocollibVersion") {
        exclude module: "mcauthlib"
    }
    shade "com.github.Steveice10:MCAuthLib:$mcauthlibVersion"

    shade "org.java-websocket:Java-WebSocket:$javawebsocketVersion"

    shade "org.apache.commons:commons-text:$apachecommonstextVersion"

    testCompile "junit:junit:$junitVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

jar {
    from {
        configurations.shade.collect {
            it.isDirectory() ? it : zipTree(it)
        }.unique()
    } {
        exclude "META-INF/**"
        exclude sourceSets.main.output.asFileTree.collect {
            return it.getPath().split("java/main/").last()
        }
    }

    manifest {
        attributes "Implementation-Title": "DaPorkchop_",
                "Implementation-Version": version,
                "Main-Class": "net.daporkchop.toobeetooteebot.Bot"
    }
}

publishing {
    repositories {
        if (project.hasProperty("daporkchopMavenUsername") && project.hasProperty("daporkchopMavenPassword")) {
            maven {
                credentials {
                    username = "$daporkchopMavenUsername"
                    password = "$daporkchopMavenPassword"
                }

                name = "DaPorkchop_"
                url = version.endsWith("SNAPSHOT") ? "https://maven.daporkchop.net/snapshot/" : "https://maven.daporkchop.net/release/"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = "toobeetooteebot"
            version = project.version

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

publish.dependsOn(publishToMavenLocal)
build.dependsOn(publish)
